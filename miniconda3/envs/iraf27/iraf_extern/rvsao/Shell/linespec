#!/data/oir/IRAF2.14.1/iraf/bin.redhat/ecl.e -f
# This script runs RVSAO RVSAO.LINESPEC from a Unix command line
# Replace the path in the first line with one appropriate to your machine
# March 31, 2009
# By Doug Mink

string uprm, hm, tempcom

# Set machine type appropriately
set arch=".redhat"

# Set the terminal type.
if (envget("TERM") == "sun") {
    stty gterm
} else if (envget("TERM") == "xterm") {
    stty xterm
} else if (envget("TERM") == "network") {
    stty vt100
} else {
    stty xterm
}

# Set the IRAF home and parameter directories
if (defvar("irafhome")) {
    hm = envget("irafhome")
    i = strlen(hm)
    if (substr(hm, i, i) == "/")  
        set (home = hm)
    else
        set (home = hm // "/")
    set uparm = "home$uparm/"
    }
else {
    hm = envget ("HOME")
    set (home = hm // "/IRAF/")
    set uparm = "home$uparm/"
    }

if (defvar("UPARM")) {
    uprm = envget("UPARM")
    i = strlen(uprm)
    if (substr(uprm, i, i) == "/")  
        set (uparm = uprm)
    else
        set (uparm = uprm // "/")
  }

# Set the user name which may appear on graphical output
if (envget("USER") != "") {
    set user = envget("USER")
    }

if (args == "") {
    print ("LINESPEC: Make an emission line template spectrum given a line list")
    print ("usage: linespec linefile specfile [other arguments]")
    print ("       linespec help  for IRAF help")
    print ("       linespec dpar  to dump parameters")
    print ("       linespec epar  to edit parameters")
    print ("       linespec lpar  to list parameters")
    }
else if (args == "help") {
    tempcom = mktemp ("templinespec")
    print ("help linespec", > tempcom)
    cl (< tempcom)
    delete (tempcom)
    }

# Edit linespec parameters using epar
else if (args == "epar") {
    tempcom = mktemp ("templinespec")
    print ("rvsao motd-\nepar linespec", > tempcom)
    cl (< tempcom)
    delete (tempcom)
    }

# Dump linespec parameters using dpar
else if (args == "dpar") {
    tempcom = mktemp ("templinespec")
    print ("rvsao motd-\ndpar linespec", > tempcom)
    cl (< tempcom)
    delete (tempcom)
    }

# List linespec parameters using lpar
else if (args == "lpar") {
    tempcom = mktemp ("templinespec")
    print ("rvsao motd-\nlpar linespec", > tempcom)
    cl (< tempcom)
    delete (tempcom)
    }

# Execute linespec using the arguments on the command line
else {
    tempcom = mktemp ("templinespec")
    printf ("rvsao motd-\nlinespec %s\n", args, > tempcom)
    cl (< tempcom)
    delete (tempcom)
    }
logout

