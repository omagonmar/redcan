# FC_VMIN[RD] -- Evaluate the minimum value of an array. It is assumed
# that the number of points in the array is always greater than zero.

PIXEL procedure fc_vmin$t (a, npts)

PIXEL	a[npts]			# input array
int	npts			# number of points

int	i
PIXEL	result

begin
	result = a[1]
	do i = 2, npts
	    result = min (result, a[i])
	return (result)
end


# FC_VMAX[RD] -- Evaluate the maximum value of an array. It is assumed
# that the number of points in the array is always greater than zero.

PIXEL procedure fc_vmax$t (a, npts)

PIXEL	a[npts]			# input array
int	npts			# number of points

int	i
PIXEL	result

begin
	result = a[1]
	do i = 2, npts
	    result = max (result, a[i])
	return (result)
end


# FC_VAVG[RD] -- Evaluate the average of an array. It is assumed that the
# number of points in the array is always greater than zero.

PIXEL procedure fc_vavg$t (a, npts)

PIXEL	a[npts]			# input array
int	npts			# number of points

int	i
PIXEL	sum

begin
	sum = 0.0
	do i = 1, npts
	    sum = sum + a[i]
	return (sum / npts)
end


# FC_VMEDIAN[RD] -- Evaluate the median of an array. It is assumed that the
# number of points in the array is always greater than zero.

PIXEL procedure fc_vmed$t (a, npts)

PIXEL	a[npts]			# input array
int	npts			# number of points

begin
	if (mod (npts, 2) == 0)
	    return (a[int (npts / 2)])
	else
	    return (a[int (npts / 2) + 1])
end


# FC_VMODE[RD] -- Evaluate the mode of an array. It is assumed that the
# number of points in the array is always greater than zero.

PIXEL procedure fc_vmode$t (a, npts, nhist)

PIXEL	a[npts]			# input array
int	npts			# number of points
int	nhist			# number of histograms

PIXEL	result

begin
	result = INDEF
	return (result)
end


# FC_VSIGMA[RD] -- Evaluate the standard deviation of an array. It is assumed
# that the number of points in the array is always greater than zero.

PIXEL procedure fc_vsigma$t (a, npts)

PIXEL	a[npts]			# input array
int	npts			# number of points

int	i
PIXEL	sum, sumsq

begin
	sum   = 0.0
	sumsq = 0.0

	do i = 1, npts {
	    sum = sum + a[i]
	    sumsq = sumsq + a[i] * a[i]
	}

	sum   = sum   / npts
	sumsq = sumsq / npts

	return (sqrt (sumsq - sum * sum))
end
